plugins {
    id 'org.springframework.boot' version '2.4.7'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id "io.freefair.lombok" version "5.3.3.3"
}

group = 'no.fint'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 11

repositories {
    mavenCentral()
    repositories {
        maven {
            url 'https://repo.fintlabs.no/releases'
        }
    }
    mavenLocal()
}


dependencies {
    compile('no.fint:fint-oauth-token-service:1.6.1')
    compile('no.fint:fint-event-model:3.0.1')
    compile('no.fint:fint-portal-api:3.12.0-rc-7')

    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('io.swagger:swagger-annotations:1.6.2')

    testCompile('no.fint:fint-test-utils:0.0.6')
    testCompile("cglib:cglib-nodep:3.3.0")
    testCompile("org.spockframework:spock-spring")
    testCompile("org.spockframework:spock-core")
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

task copyExternalDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/external"
    exclude '**/fint-*.jar'
}

task copyFintDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/fint"
    include '**/fint-*.jar'
}

build.dependsOn(copyExternalDependencies)
build.dependsOn(copyFintDependencies)

jar {
    enabled = true
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Main-Class": "no.fint.Application",
                "Specification-Version": "${version}",
                "Implementation-Version": "${version}",
                "Class-Path": configurations.runtime.collect { it.getName() }.join(' ')
        )
    }
}

bootJar {
    enabled = false
}
